--------------------------------------------------------------------------------
-- dialogtaskbar.exw
--------------------------------------------------------------------------------
-- IupDialog example including a Taskbar
-- Several buttons execute actions
--------------------------------------------------------------------------------
-- Version 4.0.5.1
-- Dated 2020.09.11
-- Author C A Newbould
-- Changes: documented in common format
--------------------------------------------------------------------------------
-- includes
--------------------------------------------------------------------------------
include iup.ew
include std/text.e -- for sprint
include std/math.e -- for round
--------------------------------------------------------------------------------
-- constants
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
-- types
--------------------------------------------------------------------------------
type Ihandle(object x)
    return 1
end type
--------------------------------------------------------------------------------
-- variables
--------------------------------------------------------------------------------
atom increment = 0.01
Ihandle progressbar1
Ihandle btn_pause
Ihandle timer
--------------------------------------------------------------------------------
-- routines
--------------------------------------------------------------------------------
function time_cb()
    Ihandle dlg = IupGetHandle("mydialog")
    atom value = IupGetFloat(progressbar1, "VALUE")
    value += increment
    if (value > 1) then value = 0 end if  /*  start over */
    IupSetAttribute(progressbar1, "VALUE",  sprint(value))
    IupSetInt(dlg, "TASKBARPROGRESSVALUE", round(100*value))
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_pause_cb()
    Ihandle dlg = IupGetHandle("mydialog")
    if (not IupGetInt(timer, "RUN")) then
        IupSetAttribute(timer, "RUN", "YES")
        IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPause")
        IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "NORMAL")
    else
        IupSetAttribute(timer, "RUN", "NO")
        IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPlay")
        IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "PAUSED")
    end if
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_restart_cb()
    Ihandle dlg = IupGetHandle("mydialog")
    IupSetAttribute(progressbar1, "VALUE", "0")
    IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "NOPROGRESS")
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_accelerate_cb()
    increment  = 2
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_decelerate_cb()
    increment /= 2
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_indeterminate_cb()
  Ihandle dlg = IupGetHandle("mydialog")
  IupSetAttribute(timer, "RUN", "NO")
  IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPlay")
  IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "INDETERMINATE")
  return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_noprogress_cb()
    Ihandle dlg = IupGetHandle("mydialog")
    IupSetAttribute(timer, "RUN", "NO")
    IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPlay")
    IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "NOPROGRESS")
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function btn_error_cb()
    Ihandle dlg = IupGetHandle("mydialog")
    IupSetAttribute(timer, "RUN", "NO")
    IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPlay")
    IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "ERROR")
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
procedure main()
    Ihandle  dlg,  vbox,  hbox
    Ihandle  btn_restart,  btn_accelerate,  btn_decelerate
    Ihandle  btn_indeterminate,  btn_noprogress,  btn_error
    IupOpen()                  /*  IUP initialization */
    timer = IupTimer()
    IupSetCallback(timer, "ACTION_CB", Icallback("time_cb"))
    IupSetAttribute(timer, "TIME", "100")
    progressbar1 = IupProgressBar()
    IupSetAttribute(progressbar1, "EXPAND", "YES")
    IupSetAttribute(progressbar1, "DASHED", "YES")
    btn_restart = IupButton()
    btn_pause = IupButton()
    btn_accelerate = IupButton()
    btn_decelerate = IupButton()
    btn_indeterminate = IupButton()
    btn_noprogress = IupButton()
    btn_error = IupButton()
    IupImageLibOpen()
    IupSetAttribute(btn_restart, "IMAGE", "IUP_MediaGotoBegin")
    IupSetAttribute(btn_restart, "TIP", "Restart")
    IupSetAttribute(btn_pause, "IMAGE", "IUP_MediaPause")
    IupSetAttribute(btn_pause, "TIP", "Play/Pause")
    IupSetAttribute(btn_accelerate, "IMAGE", "IUP_MediaForward")
    IupSetAttribute(btn_accelerate, "TIP", "Accelerate")
    IupSetAttribute(btn_decelerate, "IMAGE", "IUP_MediaRewind")
    IupSetAttribute(btn_decelerate, "TIP", "Decelerate")
    IupSetAttribute(btn_indeterminate, "IMAGE", "IUP_ToolsSettings")
    IupSetAttribute(btn_indeterminate, "TIP", "Indeterminate" )
    IupSetAttribute(btn_noprogress, "IMAGE", "IUP_MessageInfo")
    IupSetAttribute(btn_noprogress, "TIP", "No Progress")
    IupSetAttribute(btn_error, "IMAGE", "IUP_MessageError")
    IupSetAttribute(btn_error, "TIP", "Error")
    hbox = IupHbox
         ( {
           IupFill(),
           btn_pause,
           btn_restart,
           btn_decelerate,
           btn_accelerate,
           btn_noprogress,
           btn_indeterminate,
           btn_error,
           IupFill()
           }
         )
    vbox = IupHbox({IupVbox({progressbar1, hbox, NULL}), NULL})
    IupSetAttribute(vbox, "MARGIN", "10x10")
    IupSetAttribute(vbox, "GAP", "5")
    dlg = IupDialog(vbox)
    IupSetAttribute(dlg, "TITLE", "IupDialog: Progress bar in the Windows 7 Taskbar")
    IupSetCallback(btn_pause, "ACTION", Icallback("btn_pause_cb"))
    IupSetCallback(btn_restart, "ACTION", Icallback("btn_restart_cb"))
    IupSetCallback(btn_accelerate, "ACTION", Icallback("btn_accelerate_cb"))
    IupSetCallback(btn_decelerate, "ACTION", Icallback("btn_decelerate_cb"))
    IupSetCallback(btn_indeterminate, "ACTION", Icallback("btn_indeterminate_cb"))
    IupSetCallback(btn_noprogress, "ACTION", Icallback("btn_noprogress_cb"))
    IupSetCallback(btn_error, "ACTION", Icallback("btn_error_cb"))
    IupSetAttribute(dlg, "RASTERSIZE", "420x")
    IupSetAttribute(dlg, "TASKBARPROGRESS", "YES")
    IupSetAttribute(dlg, "TASKBARPROGRESSSTATE", "NORMAL")
    IupSetHandle("mydialog", dlg)
    IupShowXY(dlg, IUP_CENTER, IUP_CENTER)
    IupSetAttribute(timer, "RUN", "YES")
    IupMainLoop()
    IupClose()
end procedure
--------------------------------------------------------------------------------
-- execution
--------------------------------------------------------------------------------
main()
--------------------------------------------------------------------------------
-- Previous versions
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
