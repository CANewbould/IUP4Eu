--------------------------------------------------------------------------------
-- Demonstration: item.exw
--------------------------------------------------------------------------------
-- Notes:
--
-- Tested successfully on v4.0.5 and on v4.1.0(32-bit and 64-bit)
--------------------------------------------------------------------------------
--/*
--= Program: (iup4eu)(demos)item.exw
-- Description: program to exemplify menu items
------
--[[[Version: 4.0.5.0
--Euphoria Versions: 4.0.5 upwards
--Author: C A Newbould
--Date: 2020.10.17
--Status: operational
--Changes:]]]
--* set out in standard arrangement
--
--==IupItem Example in Euphoria
--
-- This example displays a range of Menu Items with different actions
-- associated with each.
--
-- The example also illustrates the use of key short cuts.
--
------
--------------------------------------------------------------------------------
--/*
--==Interface
--*/
--------------------------------------------------------------------------------
--/*
--=== Includes
--*/
--------------------------------------------------------------------------------
include iup.ew
--------------------------------------------------------------------------------
--
--=== Constants
--
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
--
--=== Euphoria types
--
--------------------------------------------------------------------------------
type Ihandle(object x)
    return atom(x)
end type
--------------------------------------------------------------------------------
--
--=== Variables
--
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
--
--=== Routines
--
--------------------------------------------------------------------------------
function item_save_cb()
    IupMessage("Test","Saved!")
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function item_autosave_cb()
    string state
    Ihandle item_autosave = IupGetHandle("item_autosave")
    if (IupGetInt(item_autosave, "VALUE")) then state = "OFF"
    else state = "ON"
    end if
    IupMessage("Auto Save", state)
    IupSetAttribute(item_autosave, "VALUE", state)
    return IUP_DEFAULT
end function
--------------------------------------------------------------------------------
function item_exit_cb()
    return IUP_CLOSE
end function
--------------------------------------------------------------------------------
procedure main(string title)
    IupOpen()
    Ihandle text = IupText()
    IupSetAttribute(text, "VALUE", "This is not an empty text")
    Ihandle item_save = IupItem("Save\tCtrl+S"),   /*  this is NOT related with the Ctrl+S key callback, it will just align the text at right */
        item_autosave = IupItem("&Auto Save"),
        item_exit = IupItem("Exit", "item_exit_act")
    IupSetCallback(item_save, "ACTION", Icallback("item_save_cb"))
    IupSetAttribute(item_exit, "KEY", "x")    /*  this is NOT related with the K_X key callback, it will just underline the letter */
    IupSetCallback(item_exit, "ACTION", Icallback("item_exit_cb"))
    IupSetCallback(item_autosave, "ACTION", Icallback("item_autosave_cb"))
    IupSetAttribute(item_autosave, "VALUE", "ON")
    IupSetHandle("item_autosave", item_autosave)  /*  giving a name to a iup handle */
    Ihandle menu_atom = IupMenu({item_save, item_autosave, item_exit}),
        submenu_atom = IupSubmenu("atom", menu_atom),
        menu = IupMenu({submenu_atom})
    IupSetHandle("menu", menu)
    Ihandle btn = IupButton("Test"),
        dlg = IupDialog(IupVbox({text, btn}))
    IupSetAttribute(dlg, "MARGIN", "10x10")
    IupSetAttribute(dlg, "GAP", "10")
    IupSetAttribute(dlg, "TITLE", title)
    IupSetAttribute(dlg, "MENU", "menu")
    IupSetAttribute(dlg, "SIZE", "quarterxfifth")
    IupSetAttribute(text, "SIZE", "100x20")
    IupSetAttribute(btn, "TIP", "I don't do anything")
    IupSetAttribute(btn, "TIPBALLOON", "YES")
    IupSetAttribute(btn, "TIPBALLOONTITLE", "Warning")
    IupSetAttribute(btn, "TIPBALLOONTITLEICON", "1")
    IupSetCallback(dlg, "K_cX", Icallback("item_exit_cb"))    /*  this will also affect the IupText if at focus, since it is used for clipboard cut */
    IupSetCallback(dlg, "K_cA", Icallback("item_autosave_cb"))   /*  this will also affect the IupText if at focus, since it is used for select all */
    IupSetCallback(dlg, "K_cS", Icallback("item_save_cb"))
    IupShowXY(dlg, IUP_CENTER, IUP_CENTER)
    IupMainLoop()
    IupClose()
end procedure
--------------------------------------------------------------------------------
main("IupItem example")
--------------------------------------------------------------------------------
-- Previous versions
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
